<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" applicationComplete="init();" backgroundColor="0xFFFFFF" backgroundImage="">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
		
			import com.facebook.Facebook;
			import com.facebook.events.FacebookEvent;
			import com.facebook.net.FacebookCall;
			import com.facebook.data.events.GetEventsData;
			import com.facebook.commands.events.GetEvents;
			import com.facebook.utils.FacebookSessionUtil;
			
			private var fbook:Facebook;
			private var session:FacebookSessionUtil;
			
			private var API_KEY:String = "76b12b2c46e0a4f255fc9a50a575c222";
			private var SECRET_KEY:String = "b1318cdbee3edc07fc5e14781dfc2478";
			 
			[Bindable] private var facebookEvents:ArrayCollection;
			
			private function init():void {
				session=new FacebookSessionUtil(API_KEY, SECRET_KEY, loaderInfo);
				session.addEventListener(FacebookEvent.CONNECT, onConnect);
				fbook=session.facebook;
			}
			
			private function onConnect(e:FacebookEvent):void {
				status.text = "Facebook API Ready";	
			}
			
			private function doneLoggingIn():void {
				session.validateLogin();
			}
		
			private function getEvents():void {
				var call:FacebookCall = fbook.post(new GetEvents(fbook.uid));
				call.addEventListener(FacebookEvent.COMPLETE, handleGetEventsResponse);
			}
		
			private function handleGetEventsResponse(e:FacebookEvent):void {
				var eventsResponseData:GetEventsData = e.data as GetEventsData;
				
				if (!eventsResponseData || e.error){ // an error occurred
					status.text = "Error";
					return;
				}
				
				facebookEvents = new ArrayCollection();
				
				for(var i:int = 0; i < eventsResponseData.eventCollection.length; i++){
					facebookEvents.addItem(eventsResponseData.eventCollection.getItemAt(i));	
				}
			
			}
						
		]]>
	</mx:Script>
	
	<mx:Button label="1. Login to Facebook" click="session.login();" />
	<mx:Button label="2. Done logging in" click="doneLoggingIn()" />
	<mx:Button label="3. Browse Events" click="getEvents()" />
	
	<mx:Label id="status" text="Please log in" />
	
	<mx:List id="eventsDataGrid" dataProvider="{facebookEvents}" width="500" height="400">
		<mx:itemRenderer>
			<mx:Component>
				<mx:Canvas height="150" horizontalScrollPolicy="off" verticalScrollPolicy="off">
					<mx:Image source="{data.pic}" x="5" y="5" />
					<mx:Label text="{data.name}" x="115" y="5" fontWeight="bold" />
					<mx:Label text="Where: {data.location}" x="115" y="25" />
					<mx:Label text="When: {data.start_time}" x="115" y="45" />
				</mx:Canvas>
			</mx:Component>
		</mx:itemRenderer>
	</mx:List>
	

</mx:Application>